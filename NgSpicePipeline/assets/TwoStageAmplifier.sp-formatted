circuit: TwoStageAmplifier
.include ../assets/45nm_CS.pm
.option TEMP=27C

V1 net14 0 dc 1
V2 net10 0 dc 1
V3 net4 0 dc 2.4
V4 net3 0 dc 2.1
V5 net2 0 dc 1.5
V6 net9 0 dc 700m
V7 vdd 0 dc 3.3

mn0 out1 net14 0 0 NMOS W=2.4u L=45n
mn1 out2 net10 0 0 NMOS W=2.4u L=45n

mn9 out4 net2 net11 0 NMOS W=58.5u L=117n
mn6 out3 net2 net6 0 NMOS W=58.5u L=117n

mn8 net11 net13 net8 0 NMOS W=25u L=45n
mn5 net6 net1 net8 0 NMOS  W=25u L=45n

mn7 net8 net9 0 0 NMOS W=52u L=45n

V8 net13 0 dc 1.2 ac 1
V9 net1 0 dc 1.2 ac 1

mp5 out1 out3 vdd vdd PMOS W=6u L=45n
mp4 out2 out4 vdd vdd PMOS W=6u L=45n

mp1 net12 net4 vdd vdd PMOS W=13u L=45n
mp0 net7 net4 vdd vdd PMOS W=13u L=45n

mp3 out4 net3 net12 vdd PMOS W=94u L=180n
mp2 out3 net3 net7 vdd PMOS W=94u L=180n

C4 out3 out1 14p
C3 out4 out2 14p
C1 out2 0 5p
C0 out1 0 5p

.control

let w_start = 25u
let w_stop = 30u
let delta_w = 2u
let w_test = w_start
let w2_start = 52u
let w2_stop = 55.5u
let delta_w2 = 2u
let w2_test = w2_start
let w1_start = 6u
let w1_stop = 9u
let delta_w1 = 1.5u
let w1_test = w1_start

while w_test le w_stop
	alter @mn5[w] = w_test
	alter @mn8[w] = w_test
	while w2_test le w2_stop
		alter @mp5[w]= w2_test
		alter @mp4[w]= w2_test
		while w1_test le w1_stop
		    alter @mn7[w]= w1_test
			op 
			let id2 = @mp4[id]
			let id1 = @mn8[id]
			let pw = (id1+id2)*3.3
			let av = v(out2)/v(net1)
			wrdata ../out/w0.csv w_test
			wrdata ../out/w1.csv w1_test
			wrdata ../out/w2.csv w2_test
			wrdata ../out/pw.csv pw
			wrdata ../out/a0.csv av

			ac dec 1000 1G 100G
			let resp = db(v(out2)/v(net1)) 
			let measurement_point = vecmax (resp) - 3.0
			meas AC upper_3dB WHEN resp = measurement_point 
			print upper_3dB >> ../out/bw.csv
			
			set appendwrite
			let w1_test = w1_test + delta_w1
		end
		let w2_test = w2_test + delta_w2
		let w1_test = w1_start
	end
	let w_test = w_test + delta_w
	let w2_test = w2_start
end
.endc